{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Archery Accuracy Gamma Prior in `RevBayes`\n",
    "\n",
    "## Background\n",
    "\n",
    "This Jupyter notebook is intended to demonstrate how to set up a model in RevBayes. The archery example is based on [Paul Lewis](http://phylogeny.uconn.edu/)' lecutres from the [Workshop on Molecular Evolution](https://molevol.mbl.edu/). His [Bayesian lecture slides](http://hydrodictyon.eeb.uconn.edu/people/plewis/downloads/wh2017/Bayesian-Lewis-23July2017.pdf) use archery to set up the concept of the prior and the data. [Tracy Heath](http://phyloworks.org/) extended this model for RevBayes and also provided [lecture slides online](https://www.slideshare.net/trayc7/integrative-bayesian-analysis-in-revbayes).\n",
    "\n",
    "## Objective\n",
    "\n",
    "The goal of this example is to specify a model on an archer's accuracy and precision. We will measure accuracy as `d` = the distance from the bullseye. For our analysis, we will assume that `d ~ Gamma(alpha, beta)`. For this analysis, we will re-paramterize the Gamma so that we focus on the mean and variance of the distribution. \n",
    "\n",
    "To compute the mean and variance of a gamma, we simply have to consider:\n",
    "\n",
    "```\n",
    "mean = alpha / beta\n",
    "var = alpha / beta^2\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Simulate Data\n",
    "First we will simulate the the \"observed\" data under the *true* gamma distribution. This will also allow us to compare our estimated parameters to their true values."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The true accuracy and variance correspond to the true mean and variance of the gamma distribution."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "true_accuracy = 35.0\n",
    "true_variance = 4.0"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We compute the true shape (alpha) and rate (beta) of the gamma distribution from the variables above."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "true_alpha = (true_accuracy^2) / true_variance\n",
    "true_beta = true_accuracy / true_variance"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now generate 6 shots (that's how many arrows I have) from the true gamma distribution. This uses the `rgamma()` function which, like the function of the same name in `R` returns a vector of random draws from the gamma."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "num_shots = 6\n",
    "observed_shots = rgamma(num_shots, true_alpha, true_beta)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Define the Prior Model\n",
    "Now that we have our observed data entered, we can set up our model\n",
    "\n",
    "### Stochastic Nodes\n",
    "\n",
    "In this model we have 2 stochastic nodes, the mean and variance of the gamma distribution. We are calling these nodes `mean` and `var`. \n",
    "\n",
    "#### The mean\n",
    "We are assuming a uniform prior on the mean of the gamma distribution. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "mean ~ dnUnif(10,40)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### The Variance\n",
    "We assume the variance comes from a Gamma distribution"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "var ~ dnGamma(20,2)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Deterministic Nodes\n",
    "#### Computing the alpha and beta of the gamma model\n",
    "\n",
    "Because we are putting priors on the mean and variance, we have to use deterministic nodes to get the shape and rate of the gamma distribution on archery accuracy."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "alpha := (mean * mean) / var\n",
    "beta := mean / var"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### The Data\n",
    "#### Create the node for the data\n",
    "Now we will create the stochastic node representing the data (d is the distance from the bullseye) for 6 fired shots. To do this, we will put the data `d` in a vector using a `for` loop. Within the loop, we will instantiate `d[i]` for each fire shot as a stochastic node generated by a gamma distribution. Then, we will `clamp()` each node to the corresponding observed shot."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "for(i in 1:num_shots){\n",
    "    d[i] ~ dnGamma(alpha,beta)\n",
    "    d[i].clamp(observed_shots[i])\n",
    "}"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## The Model Object\n",
    "Now that our model is fully specified and the observed data are associated with their clamped node, we can create the model object. This is a workspace object that allows us to create a container holding our full graphical model. Within the workspace, we can then use this object (called `mymodel`)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "mymodel = model(beta)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Visualize the Model Graph\n",
    "In theory, it would be great to view graphics directly in the notebook. But the graphviz plotting in python does not display the graph in the RevBayes kernel.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "mymodel.graph(\"archery_model.dot\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Since you cannot yet visualize the output in the RevBayes jupyter notebook, go to a python jupyter notebook so see some ways to view the model and summarized the output: https://github.com/phyloworks/revbayes-workshop2017/blob/master/archery-model/visualize-archery-model-py.ipynb"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Moves\n",
    "Now we must add some moves on the stochastic nodes `mean` and `var`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "moves[1] = mvSlide(mean,delta=1.0,weight=3)\n",
    "moves[2] = mvScale(var,lambda=1000,weight=3)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Monitors"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "monitors[1] = mnModel(filename=\"archery_mcmc_1.log\",printgen=10, separator = TAB)\n",
    "monitors[2] = mnScreen(printgen=1000, mean, var)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## MCMC"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "mymcmc = mcmc(mymodel, monitors, moves,nruns=1)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Run"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "   Running MCMC simulation\n",
      "   This simulation runs 1 independent replicate.\n",
      "   The simulator uses 2 different moves in a random move schedule with 6 moves per iteration\n",
      "\n",
      "Iter        |      Posterior   |     Likelihood   |          Prior   |           mean   |            var   |    elapsed   |        ETA   |\n",
      "------------------------------------------------------------------------------------------------------------------------------------------\n",
      "0           |       -105.463   |       -100.334   |       -5.12891   |        10.4354   |       9.018023   |   00:00:00   |   --:--:--   |\n",
      "1000        |       -19.4502   |       -13.5597   |       -5.89055   |       36.15045   |       12.50261   |   00:00:00   |   --:--:--   |\n",
      "2000        |       -18.3318   |       -13.2266   |       -5.10521   |       36.44617   |       9.376693   |   00:00:00   |   00:00:00   |\n",
      "3000        |       -18.7826   |       -13.2619   |       -5.52073   |       32.87039   |       7.645878   |   00:00:00   |   00:00:00   |\n",
      "4000        |       -17.8776   |        -12.722   |       -5.15562   |       34.30889   |       10.22051   |   00:00:00   |   00:00:00   |\n",
      "5000        |         -18.19   |       -13.0763   |       -5.11365   |       36.20417   |       9.812377   |   00:00:00   |   00:00:00   |\n",
      "6000        |       -18.0122   |       -12.7997   |       -5.21256   |       34.34927   |       10.55405   |   00:00:00   |   00:00:00   |\n",
      "7000        |       -17.7768   |       -12.6637   |       -5.11305   |       35.57886   |       9.802827   |   00:00:00   |   00:00:00   |\n",
      "8000        |       -17.4169   |        -11.734   |       -5.68288   |       34.51065   |       7.343098   |   00:00:00   |   00:00:00   |\n",
      "9000        |       -17.9577   |       -11.5177   |       -6.44007   |       35.43687   |       6.367643   |   00:00:00   |   00:00:00   |\n",
      "10000       |       -20.8765   |       -13.8704   |        -7.0061   |       36.11161   |       14.40778   |   00:00:00   |   00:00:00   |\n",
      "11000       |       -17.4209   |        -12.277   |       -5.14392   |       35.20605   |       8.894387   |   00:00:00   |   00:00:00   |\n",
      "12000       |       -19.5796   |       -13.3014   |       -6.27822   |       32.70851   |       6.539016   |   00:00:00   |   00:00:00   |\n",
      "13000       |          -22.8   |       -14.1638   |       -8.63611   |       36.00471   |       16.52569   |   00:00:00   |   00:00:00   |\n",
      "14000       |       -18.7805   |       -12.6484   |       -6.13202   |       33.13489   |       6.707287   |   00:00:00   |   00:00:00   |\n",
      "15000       |       -17.6239   |         -12.52   |        -5.1039   |       35.46372   |       9.446603   |   00:00:00   |   00:00:00   |\n",
      "16000       |       -20.5466   |       -13.8592   |        -6.6874   |       32.40207   |       6.130038   |   00:00:00   |   00:00:00   |\n",
      "17000       |        -19.009   |       -12.7728   |       -6.23618   |       36.47904   |       6.585979   |   00:00:00   |   00:00:00   |\n",
      "18000       |       -17.8303   |       -12.6578   |       -5.17248   |       35.00109   |       10.33209   |   00:00:00   |   00:00:00   |\n",
      "19000       |       -17.6465   |       -12.3562   |       -5.29036   |       35.77812   |       8.229495   |   00:00:00   |   00:00:00   |\n",
      "\n",
      "\n",
      "Iter        |      Posterior   |     Likelihood   |          Prior   |           mean   |            var   |    elapsed   |        ETA   |\n",
      "------------------------------------------------------------------------------------------------------------------------------------------\n",
      "20000       |       -18.7042   |       -12.3754   |       -6.32876   |        36.2329   |       6.483963   |   00:00:00   |   00:00:00   |\n",
      "21000       |       -17.5854   |       -11.4839   |       -6.10154   |       34.99728   |       6.744234   |   00:00:00   |   00:00:00   |\n",
      "22000       |       -17.5012   |       -12.2082   |       -5.29303   |       34.09779   |        8.22089   |   00:00:00   |   00:00:00   |\n",
      "23000       |       -18.5886   |       -13.4734   |       -5.11519   |       32.96276   |       9.171935   |   00:00:00   |   00:00:00   |\n",
      "24000       |       -17.9948   |        -12.881   |       -5.11381   |       35.95289   |       9.814962   |   00:00:00   |   00:00:00   |\n",
      "25000       |       -17.4403   |        -12.327   |       -5.11331   |       34.84448   |       9.199467   |   00:00:00   |   00:00:00   |\n",
      "26000       |       -18.5438   |       -13.0055   |       -5.53826   |       35.05453   |       11.67948   |   00:00:00   |   00:00:00   |\n",
      "27000       |       -19.0299   |       -13.9055   |       -5.12441   |       36.93573   |       9.951716   |   00:00:00   |   00:00:00   |\n",
      "28000       |        -17.485   |       -11.8204   |       -5.66465   |       35.31782   |       7.374429   |   00:00:00   |   00:00:00   |\n",
      "29000       |         -17.79   |       -11.2979   |       -6.49211   |       34.94598   |       6.315393   |   00:00:00   |   00:00:00   |\n",
      "30000       |       -20.0146   |       -12.1635   |        -7.8511   |       36.26607   |       5.262823   |   00:00:00   |   00:00:00   |\n"
     ]
    }
   ],
   "source": [
    "mymcmc.run(generations=30000)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {
    "collapsed": true
   },
   "source": [
    "## Summarize the data\n",
    "\n",
    "This is done in a separate python Jupyter notebook `visualize-archery-model-py.ipynb` at https://github.com/phyloworks/revbayes-workshop2017/blob/master/archery-model/visualize-archery-model-py.ipynb"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "RevBayes",
   "language": "bash",
   "name": "revbayes_kernel"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "r"
   },
   "file_extension": ".Rev",
   "help_links": [
    {
     "text": "RevBayes",
     "url": "https://revbayes.org"
    },
    {
     "text": "RevBayes Kernel",
     "url": "https://github.com/sdwfrost/revbayes_kernel"
    },
    {
     "text": "MetaKernel Magics",
     "url": "https://github.com/calysto/metakernel/blob/master/metakernel/magics/README.md"
    }
   ],
   "mimetype": "text/x-rsrc",
   "name": "RevBayes",
   "pygments_lexer": "R"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
